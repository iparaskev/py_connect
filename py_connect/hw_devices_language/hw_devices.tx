//Device
Device:
    Board | Peripheral
;

// Board meta model
Board:
    'board:'
      attrs*=BOARD_ATTRS
;

Peripheral:
    'peripheral:'
	  attrs*=PERIPHERAL_ATTRS
;

PERIPHERAL_ATTRS:
    DEVICE_ATTRS | PERIPHERAL_TYPE | PINS | I2C_ADDR | UART_BAUD
;

PERIPHERAL_TYPE:
	name="type" ":" val=PER_TYPES
;

PER_TYPES:
	"sensor" | "actuator"
;

I2C_ADDR:
	name="i2c_addr" ":" val=INT
;

UART_BAUD:
	name="uart_baudrate" ":" val=INT
;

BOARD_ATTRS:
    DEVICE_ATTRS | MEMORY | TIMERS | RTC | BATTERY | DMA | OS | CPU | PINS
;

PINS:
	name="pins" ":" val*=PIN
;

PIN:
	POWER_PIN | IO_DIGITIAL | IO_ANALOG
;

POWER_PIN:
	"- power:"
	(("name:" name=ID)
	 ("number:" number=INT)
	 ("type:" type=POWER_TYPE))#
;

POWER_TYPE:
	("gnd" | "5v" | "3v3")
;

IO_DIGITIAL:
	"- io_digital:" "->" funcs*=PIN_FUNC[","]
	(("name:" name=ID)
	 ("number:" number=INT))#
;

// Functionality of a digital pin.
PIN_FUNC:
	GPIO | I2C | SPI | UART | PWM
;

GPIO:
	type=GPIO_TYPE
;

GPIO_TYPE:
	"input" | "output" | "both"
;

I2C:
	type=I2C_TYPE "-" bus=INT
;

I2C_TYPE:
	"sda" | "scl"
;

SPI:
	type=SPI_TYPE "-" bus=INT
;

SPI_TYPE:
	"mosi" | "miso" | "sclk" | "ce"
;

UART:
	type=UART_TYPE "-" bus=INT
;

UART_TYPE:
	"tx" | "rx"
;

PWM:
	"pwm" ("at" freq=NUMBER)?
;

IO_ANALOG:
	"- io_analog:"
	(("name:" name=ID)
	 ("number:" number=INT)
	 ("vmax:" vmax=NUMBER))#
;

TIMERS:
	name="timers" ":" val=INT
;

RTC:
	name="rtc" ":" val=INT
;

BATTERY:
	name="battery" ":" val=BOOL
;

DMA:
	name="dma" ":" val=BOOL
;

OS:
	name="os" ":" val=OS_TYPE
;

OS_TYPE:
	("raspbian" | "riot")
;

CPU:
	name="cpu" ":" val=CPU_TYPE
;

CPU_TYPE:
	(("cpu_family:" cpu_family=CPU_FAMILY)
	 ("max_freq:" max_freq=NUMBER unit=FREQ_UNIT)
	 ("fpu:" fpu=BOOL))#
;

CPU_FAMILY:
	("ARM_CORTEX_M" | "ESP32" | "ARM_CORTEX_A")
;

MEMORY:
    name="memory" ":" val=MEM_TYPE
;

MEM_TYPE:
    (("ram:" ram=MEM_VALUE)?
    ("rom:" rom=MEM_VALUE)?
    ("external_memory:" external_memory=MEM_VALUE)?)#
;

MEM_VALUE:
    val=NUMBER unit=UNITS?
;

UNITS:
    ("kb" | "mb" | "gb" | "b")
;

DEVICE_ATTRS:
    VCC | NAME | OP_VOLT | NETWORK | BLUETOOTH
;

VCC:
    name="vcc" ":" val=NUMBER
;

NAME:
   name="name" ":" val=ID
;

OP_VOLT:
   name="operating_voltage" ":" val=NUMBER
;

NETWORK:
    name="network" ":"
    val*=NETWORK_TYPE
;

NETWORK_TYPE:
    WIFI | ETHERNET
;

WIFI:
	"- wifi:" 
	(("name:" name=ID)
	 ("freq:" freq*=NUMBER[","] unit=FREQ_UNIT)?)#
;

FREQ_UNIT:
	"ghz" | "hz"
;
ETHERNET:
	"- ethernet:"
	"name:" name=ID
;

BLUETOOTH:
	name="bluetooth" ":" val=BLUETOOTH_VAL
;

BLUETOOTH_VAL:
	"version:" version=NUMBER
;

ATTRIBUTE:
    name=ID ":" val=VAL_TYPE
;

VAL_TYPE:
    NUMBER | ID
;
